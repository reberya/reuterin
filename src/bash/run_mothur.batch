###############
# prepare environment on greatlakes
###############
#module load Bioinformatics
#module load mothur/1.42.3


###############
# prepare files
###############
# get all files from folders 
#find . -mindepth 2 -type f -print -exec mv {} . \;

# remove dash from file name
#find . -depth -name '*\-*' \
#    -execdir bash -c 'mv -- "$1" "${1/\-/.}"' bash {} \;


###############
# MOTHUR: Reducing sequencing and PCR errors
###############

# make files file
make.file(inputdir=., type=gz, prefix=reuterin)

# make contigs
make.contigs(file=reuterin.files, processors=12)


###############
# MOTHUR: Processing improved sequences
###############

# remove seqs that are ambiguous/long
screen.seqs(fasta=current, group=current, maxambig=0, maxlength=275)

# get unique sequences
unique.seqs()

# coount sequences
count.seqs(name=current, group=current)

# align sequences
align.seqs(fasta=current, reference=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/mothur_data/silva.nr_v132.align)

# Screen sequences for proper alignment
screen.seqs(fasta=current, count=current, start= 13862, end= 23444, maxhomop=8)

# Ensure only overlap region
filter.seqs(fasta=current, vertical=T, trump=.)

# remove redundancy created by trimming overlaps
unique.seqs(fasta=current, count=current)

# pre cluster
## Initially ran into an error here; this was resolved by eliminating '-' in file names
pre.cluster(fasta=current, count=current, diffs=2, processors=12)

# Remove chimeras from count file
chimera.vsearch(fasta=current, count=current, dereplicate=t)

# Remove chimeras from fasta file 
remove.seqs(fasta=current, accnos=current)

# remove non-bacterial undesirable sequences
## see README.md for why the taxonomy and reference files were chosen (hint recommended)
classify.seqs(fasta=current, count=current, reference=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/mothur_data/silva.nr_v132.align, taxonomy=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/mothur_data/silva.nr_v132.tax, cutoff=80)

# Remove the undesirable lineages (non-bacterial)
remove.lineage(fasta=current, count=current, taxonomy=current, taxon=Chloroplast-Mitochondria-unknown-Archaea-Eukaryota)

# rename current fasta and counts so can access after analyzing mock
rename.file(fasta=current, new=reuterin_A.fasta)
rename.file(count=current, new=reuterin_A.count_table)
rename.file(taxonomy=current, new=reuterin_A.taxonomy)

# summarize taxonomy
summary.tax(taxonomy=current, count=current)


###############
# MOTHUR: Assessing error rates
## Note: These have diff file names from tutorial d/t only bacterial sequences being present (no pick.pick)
## Error rate is 0.000120611
###############

# set groups for mock so can assess error rates
get.groups(count=current, fasta=current, groups=MockZymoPos)

# calculate error rate
seq.error(fasta=current, count=current, reference=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/mothur_data/zymo.mock.16S_fasta.txt, aligned=F)

# How many OTUs identified?
dist.seqs(fasta=current, cutoff=0.03)
cluster(column=current, count=current)
make.shared(list=current, count=current, label=0.03)
rarefaction.single(shared=current)


###############
# MOTHUR: Prep for analysis
###############

# remove Mock group
remove.groups(count=reuterin_A.count_table, fasta= reuterin_A.fasta, taxonomy=reuterin_A.taxonomy, groups=MockZymoPos)


###############
# OTUs
###############

# cluster into OTUs
dist.seqs(fasta=current, cutoff=0.03)
cluster(column=current, count=current)


# How many sequences in OTU from each group?
make.shared(list=current, count=current, label=0.03)

# rename shared
rename.file(shared=current, new=reuterin_otu.shared)

# Classify OTUs in each taxonomy
classify.otu(list=current, count=current, taxonomy=current, label=0.03)


###############
# Phylotype
###############

# phylotype
phylotype(taxonomy=current)

# Genus level shared file
make.shared(list=current, count=current, label=1)

# rename shared
rename.file(shared=current, new=reuterin_phylotype.shared)

# Classify phylotypes
classify.otu(list=current, count=current, taxonomy=current, label=1)


###############
# OTU - Alpha Diversity
###############

# Alpha diversity
rarefaction.single(shared=reuterin_otu.shared, calc=sobs, freq=100)

# table the number of sequences, sample coverage, number of observed OTUs, and the Inverse Simpson diversity estimate 
summary.single(shared=current, calc=nseqs-coverage-sobs-invsimpson, subsample=1495)


###############
# OTU - Beta Diversity
###############

# calculate similarity of membership and structure
dist.shared(shared=current, calc=thetayc-jclass, subsample= 1496)

# pcoa plot
pcoa(phylip=current)

# 2D nmds plot
nmds(phylip= current)

# 3D nmds plot
#nmds(phylip=current, mindim=3, maxdim=3)


###############
# OTU - PCA: all samples (except those not meeting QC)
###############

# Remove groups not in design file and those with less sequences than water control
remove.groups(shared=current, groups=WaterNeg-DNA.ExtractionCtrl-MockZymoPos-513.3-516.1)

# rename file
rename.file(shared=current, new=reuterin_earlyLate.shared)

# PCA plot for all samples
pca(shared=reuterin_earlyLate.shared)


###############
# OTU - PCA: Early samples
###############

# Remove groups not in design file and those with less sequences than water control
remove.groups(shared= reuterin_earlyLate.shared, groups=513.1S-513.2S-513.3S-513.4S-513.5S-514.1S-514.2S-514.3S-514.4S-515.1S-515.2S-515.3S-515.4S-516.1S-516.2S-517.2S-517.3S-517.4S-517.5S-518.2S-518.3S-518.4S-519.1S-519.3S-519.4S-519.5S)

# rename file
rename.file(shared=current, new=reuterin_early.shared)

# PCA plot for all samples
pca(shared=reuterin_early.shared)


###############
# OTU - PCA: Late samples
###############

# Remove groups not in design file and those with less sequences than water control
remove.groups(shared=reuterin_earlyLate.shared, groups=513.1-513.2-513.3-513.4-513.5-514.1-514.2-514.3-514.4-515.1-515.2-515.3-515.4-516.1-516.2-517.2-517.3-517.4-517.5-518.2-518.3-518.4-519.1-519.3-519.4-519.5)

# rename file
rename.file(shared=current, new=reuterin_late.shared)

# PCA plot for all samples
pca(shared=reuterin_late.shared)


###############
# OTU - PCA: Late samples (remove 516.1S as outlier)
###############

# Remove groups not in design file and those with less sequences than water control
remove.groups(shared=reuterin_late.shared, groups = 516.1S)

# rename file
rename.file(shared=current, new=reuterin_late_outRem.shared)

# PCA plot for all samples
pca(shared=reuterin_late_outRem.shared)


###############
# OTU - Differences in genotype - all samples
###############

# Remove groups not in design file and those with less sequences than water control
remove.groups(shared=reuterin_earlyLate.shared, groups=516.1S)

# calculate similarity of membership and structure
dist.shared(shared= reuterin_earlyLate.shared, calc=thetayc-jclass, subsample= 1496)

# Genotype comparison - AMOVA - all samples (Is clustering signif different b/t genotypes?)
amova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)

# Genotype comparison - Homova - all samples (Is there a difference in variation b/t these samples?)
homova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)


###############
# OTU - Differences in genotype - early samples
###############

# calculate similarity of membership and structure
dist.shared(shared= reuterin_early.shared, calc=thetayc-jclass, subsample= 1496)

# Genotype comparison - AMOVA - early samples (Is clustering signif different b/t genotypes?)
amova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)

# Genotype comparison - Homova - early samples (Is there a difference in variation b/t these samples?)
homova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)


###############
# OTU - Differences in genotype - late samples (no 516.1S)
###############

# calculate similarity of membership and structure
dist.shared(shared=reuterin_late_outRem.shared, calc=thetayc-jclass, subsample= 1496)

# Genotype comparison - AMOVA - late samples (Is clustering signif different b/t genotypes?)
amova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)

# Genotype comparison - Homova - late samples (Is there a difference in variation b/t these samples?)
homova(phylip=current, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)

# pcoa plot
pcoa(phylip=current)

# Genotype comaprison - Corr axes - (which OTUs are responsible for shifting the samples along the two axes?)
corr.axes(axes=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/fq/reuterin_late_outRem.thetayc.0.03.lt.ave.pcoa.axes, shared=reuterin_late_outRem.shared, method=spearman, numaxes=2)


###############
# OTU - Differences in OTUs b/t samples - late samples (no 516.1S)
# (non-parametric T-tetst that determines whether there are any OTUs that are differentially represented between the samples)
###############

metastats(shared= reuterin_late_outRem.shared, design=/nfs/turbo/umms-shahy/seq/16s/M02127_Run495_ShahHBell_MS3078761_20210527/hannah/data/metadata/SAMPLE-GENOTYPE.txt)











